include(ExternalProject)

find_package(LibXml2 REQUIRED)
find_package(OpenSSL REQUIRED)

set(DAVIX_VERSION "0.6.4")
set(DAVIX_URL "http://grid-deployment.web.cern.ch/grid-deployment/dms/lcgutil/tar/davix")
set(DAVIX_URLHASH "SHA256=4db74681ab83307c5477d29f0680953f1e6359efed001d52a6e8cff47291165b")
set(DAVIX_PREFIX ${CMAKE_CURRENT_BINARY_DIR}/DAVIX-prefix)
set(DAVIX_LIBNAME ${CMAKE_STATIC_LIBRARY_PREFIX}davix${CMAKE_STATIC_LIBRARY_SUFFIX})

foreach(lib davix neon boost_static_internal)
  set(libname ${CMAKE_STATIC_LIBRARY_PREFIX}${lib}${CMAKE_STATIC_LIBRARY_SUFFIX})
  list(APPEND DAVIX_LIBRARIES ${DAVIX_PREFIX}/lib/${libname})
endforeach()

ExternalProject_Add(DAVIX
  URL ${DAVIX_URL}/${DAVIX_VERSION}/davix-embedded-${DAVIX_VERSION}.tar.gz
  URL_HASH ${DAVIX_URLHASH}
  PATCH_COMMAND patch -p1 -i ${CMAKE_CURRENT_SOURCE_DIR}/davix-0.6.4.patch
  CMAKE_CACHE_ARGS -DCMAKE_PREFIX_PATH:STRING=${OPENSSL_PREFIX}
  CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
  -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
  -DBOOST_EXTERNAL=OFF
  -DSTATIC_LIBRARY=ON
  -DSHARED_LIBRARY=OFF
  -DENABLE_TOOLS=OFF
  -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
  -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
  -DCMAKE_OSX_SYSROOT=${CMAKE_OSX_SYSROOT}
  -DCMAKE_OSX_DEPLOYMENT_TARGET=${CMAKE_OSX_DEPLOYMENT_TARGET}
  -DLIB_SUFFIX=
  LOG_BUILD 1 LOG_CONFIGURE 1 LOG_DOWNLOAD 1 LOG_INSTALL 1
  BUILD_BYPRODUCTS ${DAVIX_LIBRARIES})

if(builtin_openssl)
  add_dependencies(DAVIX OPENSSL)
endif()

list(APPEND DAVIX_LIBRARIES ${LIBXML2_LIBRARIES} ${OPENSSL_LIBRARIES} ${CMAKE_DL_LIBS})

include(CheckCXXSymbolExists)
check_cxx_symbol_exists("clock_gettime" "time.h" _have_clock_gettime)

include(CheckLibraryExists)
if(NOT _have_clock_gettime)
  check_library_exists(rt clock_gettime "" _need_rt_library)
  list(APPEND DAVIX_LIBRARIES rt)
endif()

set(DAVIX_INCLUDE_DIR  ${DAVIX_PREFIX}/include/davix CACHE INTERNAL "" FORCE)
set(DAVIX_INCLUDE_DIRS ${DAVIX_PREFIX}/include/davix CACHE INTERNAL "" FORCE)
set(DAVIX_LIBRARY ${DAVIX_PREFIX}/lib/${DAVIX_LIBNAME} CACHE INTERNAL "" FORCE)
set(DAVIX_LIBRARIES ${DAVIX_LIBRARIES} CACHE INTERNAL "" FORCE)

add_library(davix INTERFACE)
target_include_directories(davix INTERFACE $<BUILD_INTERFACE:${DAVIX_INCLUDE_DIR}>)
target_link_libraries(davix INTERFACE $<BUILD_INTERFACE:${DAVIX_LIBRARIES}>)
add_dependencies(davix DAVIX)

add_library(Davix::Davix ALIAS davix)

set_property(GLOBAL APPEND PROPERTY ROOT_BUILTIN_TARGETS DAVIX)
